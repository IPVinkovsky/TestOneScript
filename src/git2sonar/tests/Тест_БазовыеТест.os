#Использовать "../src/cmd"
#Использовать "../src/core"
#Использовать asserts

Перем СсылкаНаПроект;
Перем КаталогПроекта;

&Тест
Процедура ПроверитьДатуДляИстории() Экспорт
	
	Дата = Дата(2020, 1, 1);
	ДатаДляИстории = УправлениеАнализом.ДатаДляИстории(Дата);
	
	Ожидаем.Что(ДатаДляИстории).Равно("2020-01-01");
	
КонецПроцедуры

&Тест
Процедура ПроверитьСпискаКоммитов() Экспорт
	
	КлонироватьПроект(СсылкаНаПроект);
	
	// проверяем на ветке master
	Ветка = "master";
	ДатаСреза = УправлениеАнализом.ДатаДляИстории(Дата(2020, 9, 1));
	КоллекцияКоммитов = УправлениеАнализом.СписокКоммитов(КаталогПроекта, ДатаСреза, Ветка);
	
	Ожидаем.Что(КоллекцияКоммитов.Количество()).Больше(1);
	Коммит1 = КоллекцияКоммитов[0];
	Ожидаем.Что(Коммит1.Коммит).Равно("4169be4");
	
	Коммит2 = КоллекцияКоммитов[1];
	Ожидаем.Что(Коммит2.Коммит).Равно("e155496");
	
	ДатаСреза = УправлениеАнализом.ДатаДляИстории(Дата(2020, 9, 10));
	КоллекцияКоммитов = УправлениеАнализом.СписокКоммитов(КаталогПроекта, ДатаСреза, Ветка);
	Ожидаем.Что(КоллекцияКоммитов.Количество()).Больше(0);
	
	Коммит1 = КоллекцияКоммитов[0];
	Ожидаем.Что(Коммит1.Коммит).Равно("e155496");
	
КонецПроцедуры

&Тест
Процедура ПроверитьВходящиеПараметрыКоманды() Экспорт
	
	КлонироватьПроект(СсылкаНаПроект);
	
	НастройкиЗапуска = Новый Структура;
	НастройкиЗапуска.Вставить("ПутьДоКаталогаПроекта", КаталогПроекта);
	НастройкиЗапуска.Вставить("КаталогиИсходныхКодов", "src");
	НастройкиЗапуска.Вставить("КлючПроекта", "repo");
	НастройкиЗапуска.Вставить("АдресСонара", "http://localhost:9000");
	НастройкиЗапуска.Вставить("ТокенСонара", "d91e860e57084ad681d1952d407fb343f6f504e3");
	НастройкиЗапуска.Вставить("ДатаАнализа", ТекущаяДата());
	НастройкиЗапуска.Вставить("ВеткаПроекта", "master");
	
	Отказ = УправлениеАнализом.ВходящиеНастройкиКомандыУказаныКорректно(НастройкиЗапуска);
	Ожидаем.Что(Отказ).ЕстьЛожь();
	
	НастройкиЗапуска.Вставить("ПутьДоКаталогаПроекта", "123");
	Отказ = УправлениеАнализом.ВходящиеНастройкиКомандыУказаныКорректно(НастройкиЗапуска);
	Ожидаем.Что(Отказ).ЕстьИстина();
	
КонецПроцедуры

Процедура КлонироватьПроект(Ссылка)
	КомандаClone = Новый Команда();
	КомандаClone.УстановитьКоманду("git");
	КомандаClone.ДобавитьПараметр("clone");
	КомандаClone.ДобавитьПараметр(Ссылка);
	КомандаClone.ДобавитьПараметр("repo");
	КомандаClone.УстановитьРабочийКаталог("build");
	КомандаClone.Исполнить();
КонецПроцедуры

СсылкаНаПроект = "https://github.com/silverbulleters/git2sonar.git";
КаталогПроекта = "build/repo";